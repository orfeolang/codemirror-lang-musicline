@top Program { line* }

line {
    hws*
    ( LineComment | shortMusiclineFormat | longMusiclineFormat )
    ( newline | eof )
}

shortMusiclineFormat {
    Timepoint
    ( hws* | hws+ eventNoteDataShortFormat )
}

longMusiclineFormat {
    Timepoint hws+
    Voice     hws+
    (
        (
            EventMarkerId hws+ EventMarkerData
            | EventRestId
            | EventTailId
            | EventTempoId hws+ EventTempoData
        )
        hws*

        |

        (
            EventNoteId hws+ eventNoteDataLongFormat
            | EventRestedId hws+ eventRestedData
        )
    )
}

eventNoteDataShortFormat {
    EventNoteEscapeChar EventNoteEscapedData?
    | EventNoteUnescapedDataShortFormat
}

eventNoteDataLongFormat {
    EventNoteEscapeChar EventNoteEscapedData?
    | EventNoteUnescapedDataLongFormat
}

eventRestedData {
    EventRestedEscapeChar EventRestedEscapedData?
    | EventRestedUnescapedData
}

@tokens {
    LineComment              { '#' ![\n]* }

    Timepoint                { number }
    Voice                    { integer }

    EventMarkerId            { 'marker' }
    EventNoteId              { 'note' }
    EventRestId              { 'rest' }
    EventRestedId            { 'rested' }
    EventTailId              { 'tail' }
    EventTempoId             { 'tempo' }

    EventMarkerData          { $[a-zA-Z0-9'_.-]* $[a-zA-Z0-9']+ }
    EventTempoData           { number }

    EventNoteEscapeChar      { escape }
    EventNoteEscapedData     { escapedData }
    EventNoteUnescapedDataShortFormat { unescapedDataShortFormat }
    EventNoteUnescapedDataLongFormat  { unescapedDataLongFormat }

    EventRestedEscapeChar    { escape }
    EventRestedEscapedData   { escapedData }
    EventRestedUnescapedData { unescapedDataLongFormat }

    escape                   { '\\' }
    escapedData              { $[ \t]* ![ \t\n] ![\n]* }
    unescapedDataShortFormat { ![ \t\n0-9]      ![\n]* }
    unescapedDataLongFormat  { ![ \t\n]         ![\n]* }

    number                   { integer fraction? }
    integer                  { '0' | $[1-9] @digit* }
    fraction                 { '.' @digit+ }
    hws                      { $[ \t] }
    newline                  { '\n' }
    eof                      { @eof }

    @precedence { EventNoteEscapeChar, EventNoteUnescapedDataShortFormat }
    @precedence { EventNoteEscapeChar, EventNoteUnescapedDataLongFormat }
    @precedence { EventRestedEscapeChar, EventRestedUnescapedData }
}
