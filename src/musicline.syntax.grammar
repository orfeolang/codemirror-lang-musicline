@top Program { line* }

line {
    hws*
    ( LineComment | shortMusiclineFormat | longMusiclineFormat )
    ( newline | eof )
}

shortMusiclineFormat {
    Timepoint
    ( hws* | hws+ eventNoteData )
}

longMusiclineFormat {
    Timepoint hws+
    Voice     hws+
    (
        (
            EventMarkerId hws+ EventMarkerData
            | EventRestId
            | EventTailId
            | EventTempoId hws+ EventTempoData
        )
        hws*

        |

        (
            EventNoteId hws+ eventNoteData
            | EventRestedId hws+ eventRestedData
        )
    )
}

eventNoteData {
    EventNoteEscapeChar EventNoteEscapedData?
    | EventNoteUnescapedData
}

eventRestedData {
    EventRestedEscapeChar EventRestedEscapedData?
    | EventRestedUnescapedData
}

@tokens {
    LineComment              { '#' ![\n]* }

    Timepoint                { number }
    Voice                    { integer }

    EventMarkerId            { 'marker' }
    EventNoteId              { 'note' }
    EventRestId              { 'rest' }
    EventRestedId            { 'rested' }
    EventTailId              { 'tail' }
    EventTempoId             { 'tempo' }

    EventMarkerData          { $[a-zA-Z0-9'_.-]* $[a-zA-Z0-9']+ }
    EventTempoData           { number }

    EventNoteEscapeChar      { escape }
    EventNoteEscapedData     { escapedData }
    EventNoteUnescapedData   { unescapedData }

    EventRestedEscapeChar    { escape }
    EventRestedEscapedData   { escapedData }
    EventRestedUnescapedData { unescapedData }

    escape                   { '\\' }
    escapedData              { $[ \t]* ![ \t\n] ![\n]* }
    unescapedData            { ![ \t\n0-9]      ![\n]* }
    number                   { integer fraction? }
    integer                  { '0' | $[1-9] @digit* }
    fraction                 { '.' @digit+ }
    hws                      { $[ \t] }
    newline                  { '\n' }
    eof                      { @eof }

    @precedence { EventNoteEscapeChar, EventNoteUnescapedData }
    @precedence { EventRestedEscapeChar, EventRestedUnescapedData }
}
