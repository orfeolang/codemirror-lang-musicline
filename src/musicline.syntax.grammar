@top Program { line* }

line {
    hws*
    ( LineComment | longMusiclineFormat | shortMusiclineFormat )
    ( newline | eof )
}

longMusiclineFormat {
    Timepoint hws+
    Voice     hws+
    (
        (
            EventRestId                        |
            EventRestedId                      |
            EventTailId                        |
            EventTempoId hws+ EventTempoData   |
            EventMarkerId hws+ EventMarkerData
        )
        hws*

        |

        EventNoteId hws+ ( Escape EscapedNoteData | UnescapedNoteData )
    )
}

shortMusiclineFormat {
    Timepoint
    ( hws* | hws+ ( Escape EscapedNoteData | UnescapedNoteData ) )
}

@tokens {
    Timepoint         { integer fraction? }
    Voice             { integer }

    EventMarkerId     { 'marker' }
    EventNoteId       { 'note'   }
    EventRestId       { 'rest'   }
    EventRestedId     { 'rested' }
    EventTailId       { 'tail'   }
    EventTempoId      { 'tempo'  }

    EventMarkerData   { $[a-zA-Z0-9'_.-]* $[a-zA-Z0-9']+ }
    EventTempoData    { integer fraction? }

    Escape            { '\\' }
    EscapedNoteData   { $[ \t]* ![ \t\n] ![\n]* }
    UnescapedNoteData { ![ \t\n0-9]      ![\n]* }

    LineComment       { '#'              ![\n]* }

    integer           { '0' | $[1-9] @digit* }
    fraction          { '.' @digit+ }
    hws               { $[ \t] }
    newline           { '\n' }
    eof               { @eof }

    @precedence { Escape, UnescapedNoteData }
}
